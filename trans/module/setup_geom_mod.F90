MODULE SETUP_GEOM_MOD
CONTAINS
SUBROUTINE SETUP_GEOM

#include "tsmbkind.h"

USE TPM_GEN
USE TPM_DIM
USE TPM_FIELDS
USE TPM_GEOMETRY

IMPLICIT NONE

REAL_B :: ZSQM2(R%NDGL)
INTEGER_M :: IDGLU(0:R%NSMAX,R%NDGNH)
INTEGER_M :: JGL,JM

LOGICAL    :: LLP1,LLP2

!     ------------------------------------------------------------------

LLP1 = NPRINTLEV>0
LLP2 = NPRINTLEV>1

IF(LLP1) WRITE(NOUT,*) '=== ENTER ROUTINE SETUP_GEOM ==='

ALLOCATE (G%NMEN(R%NDGL))
IF(LLP2)WRITE(NOUT,9) 'G%NMEN   ',SIZE(G%NMEN   ),SHAPE(G%NMEN   )

IF (G%LREDUCED_GRID) THEN
  IF (G%LINEAR_GRID) THEN
    ZSQM2(:) = _ZERO_
  ELSE
    ZSQM2(:) = F%R1MU2(:)
  ENDIF
  G%NMEN(1) = MIN(R%NSMAX,INT(REAL(G%NLOEN(1)-1,JPRB)/(_TWO_+ZSQM2(1))))
  DO JGL=2,R%NDGNH
    G%NMEN(JGL) = MIN(R%NSMAX,MAX(G%NMEN(JGL-1),&
     &INT(REAL(G%NLOEN(JGL)-1,JPRB)/(_TWO_+ ZSQM2(JGL)))))
  ENDDO
  !       * SOUTHERN HEMISPHERE :
  G%NMEN(R%NDGL) = MIN(R%NSMAX,INT(REAL(G%NLOEN(R%NDGL)-1,JPRB)/(_TWO_+ZSQM2(R%NDGL))))
  DO JGL=R%NDGL-1, R%NDGNH+1, -1
    G%NMEN(JGL) = MIN(R%NSMAX,MAX(G%NMEN(JGL+1),&
     &INT(REAL(G%NLOEN(JGL)-1,JPRB)/(_TWO_+ ZSQM2(JGL)))))
  ENDDO
  
ELSE
  G%NMEN(:) = R%NSMAX
ENDIF

ALLOCATE(G%NDGLU(0:R%NSMAX))
IF(LLP2)WRITE(NOUT,9) 'G%NDGLU   ',SIZE(G%NDGLU   ),SHAPE(G%NDGLU   )
IDGLU(:,:) = 0
G%NDGLU(:) = 0
DO JGL=1,R%NDGNH
  DO JM=0,G%NMEN(JGL)
    IDGLU(JM,JGL) = 1
  ENDDO
ENDDO
DO JM=0,R%NSMAX
  DO JGL=1,R%NDGNH
    G%NDGLU(JM) = G%NDGLU(JM)+IDGLU(JM,JGL)
  ENDDO
ENDDO

!     ------------------------------------------------------------------
9 FORMAT(1X,'ARRAY ',A10,' ALLOCATED ',8I8)

END SUBROUTINE SETUP_GEOM
END MODULE SETUP_GEOM_MOD
