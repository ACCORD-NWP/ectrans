MODULE TRGTOL_MOD
CONTAINS
SUBROUTINE TRGTOL(PGLAT,KF_FS,KF_GP,KF_SCALARS_G,KVSET,KPTRGP,&
 &PGP,PGPUV,PGP3A,PGP3B,PGP2)

!**** *TRGTOL * - transposition of grid point data from column
!                 structure to latitudinal. Reorganize data between
!                 grid point calculations and direct Fourier Transform

!     Purpose.
!     --------


!**   Interface.
!     ----------
!        *call* *trgtol(...)

!        Explicit arguments :
!        --------------------
!           PGLAT    -  Latitudinal data ready for direct FFT (output)
!           PGP    -  Blocked grid point data    (input)

!        Implicit arguments :
!        --------------------

!     Method.
!     -------
!        See documentation

!     Externals.
!     ----------

!     Reference.
!     ----------
!        ECMWF Research Department documentation of the IFS

!     Author.
!     -------
!        MPP Group *ECMWF*

!     Modifications.
!     --------------
!        Original: 95-10-01
!        D.Dent  : 97-08-04   Reorganisation to allow
!                             NPRTRV to differ from NPRGPEW
!                : 98-06-17   add mailbox control logic (from TRLTOM)
!        =99-03-29= Mats Hamrud and Deborah Salmond
!                   JUMP in FFT's changed to 1
!                   INDEX introduced and ZCOMBUF not used for same PE
!         01-11-23  Deborah Salmond and John Hague
!                    LIMP_NOOLAP Option for non-overlapping message passing 
!                    and buffer packing
!         01-12-18  Peter Towers
!                   Improved vector performance of GTOL_PACK,GTOL_UNPACK
!         03-04-02  G. Radnoti: call barrier always when nproc>1 
!     ------------------------------------------------------------------



#include "tsmbkind.h"
USE MPL_MODULE
USE YOMGSTATS, ONLY : LSYNCSTATS

USE TPM_GEN
USE TPM_DISTR
USE TPM_TRANS

USE INIGPTR_MOD
USE PE2SET_MOD
USE MYSENDSET_MOD
USE MYRECVSET_MOD
USE ABORT_TRANS_MOD

IMPLICIT NONE

REAL_B,INTENT(OUT)   :: PGLAT(:,:)
INTEGER_M,INTENT(IN) :: KVSET(:)
INTEGER_M,INTENT(IN) :: KF_FS,KF_GP
INTEGER_M,INTENT(IN) :: KF_SCALARS_G
INTEGER_M ,OPTIONAL, INTENT(IN) :: KPTRGP(:)
REAL_B,OPTIONAL,INTENT(IN)     :: PGP(:,:,:)
REAL_B,OPTIONAL,INTENT(IN)     :: PGPUV(:,:,:,:)
REAL_B,OPTIONAL,INTENT(IN)     :: PGP3A(:,:,:,:)
REAL_B,OPTIONAL,INTENT(IN)     :: PGP3B(:,:,:,:)
REAL_B,OPTIONAL,INTENT(IN)     :: PGP2(:,:,:)

REAL_B,ALLOCATABLE :: ZCOMBUFS(:,:),ZCOMBUFR(:,:)

INTEGER_M :: ISENT    (NPROC)
INTEGER_M :: IRCVD    (NPROC)
INTEGER_M :: ISENDTOT (NPROC)
INTEGER_M :: IRECVTOT (NPROC)
INTEGER_M :: ISENDREQ (NPROC)
INTEGER_M :: IRECVREQ (NPROC)
INTEGER_M :: JRECV    (NPROC)
INTEGER_M :: JSEND    (NPROC)

!     LOCAL INTEGER SCALARS
INTEGER_M :: IERR, IFIRST, IFIRSTLAT, IGL, IGLL, ILAST,&
             &ILASTLAT, ILEN, ILRECV(NPROC), JROC, IPOS, ISETA, &
             &ISETB, IRCVTG, IRECV, IRECVD, IRECVSET, &
             &ISETV, ISEND, ISENDSET, ITAG, J, JBLK, JFLD, &
             &JGL, JK, JL, JLOOP, ISETW,  JVAR,IFLD, &
             &II,INDOFFX,IBUFLENS,IBUFLENR,INRECV, IPROC,IFLDS, &
             &INSEND,INS,INR


!     LOCAL LOGICAL SCALARS
LOGICAL   :: LLPGPUV,LLPGP3A,LLPGP3B,LLPGP2,LLPGPONLY
LOGICAL   :: LLUV(KF_GP),LLGP2(KF_GP),LLGP3A(KF_GP),LLGP3B(KF_GP)
LOGICAL :: LLDONE, LLEXIST,  LLRECV,LLINDER
INTEGER_M :: IUVLEVS(KF_GP),IUVPARS(KF_GP),IGP2PARS(KF_GP)
INTEGER_M :: IGP3APARS(KF_GP),IGP3ALEVS(KF_GP),IGP3BPARS(KF_GP),IGP3BLEVS(KF_GP)
INTEGER_M :: IUVPAR,IUVLEV,IGP2PAR,IGP3ALEV,IGP3APAR,IGP3BLEV,IGP3BPAR,IPAROFF
INTEGER_M :: IOFF,IOFF1,IOFFNS,IOFFEW,J1,J2
INTEGER_M :: INDEX(D%NLENGTF),INDOFF(NPROC),IFLDOFF(KF_FS)
INTEGER_M :: ISEND_FLD_TOTAL(NPROC),ISEND_FLD_START(NPROC),ISEND_FLD_END
INTEGER_M :: IRECV_FLD_START,IRECV_FLD_END
INTEGER_M :: ICOMBFLEN
INTEGER_M :: INUMFLDS
INTEGER_M :: IPROCS_COMM_MAX
INTEGER_M :: IGPTRSEND(2,NGPBLKS,NPRTRNS)
INTEGER_M :: IGPTRRECV(NPRTRNS)
INTEGER_M :: IGPTROFF(NGPBLKS)
!     INTEGER FUNCTIONS
INTEGER_M :: IRECVCOUNT,ISENDCOUNT
INTEGER_M :: IJPOS(NGPBLKS),IFLDA(KF_GP),JJ,JI

!     ------------------------------------------------------------------

!*       0.    Some initializations
!              --------------------
LLINDER = .FALSE.
LLPGPUV = .FALSE.
LLPGP3A = .FALSE.
LLPGP3B = .FALSE.
LLPGP2  = .FALSE.
LLPGPONLY = .FALSE.
IF(PRESENT(KPTRGP))  LLINDER = .TRUE.
IF(PRESENT(PGP))     LLPGPONLY = .TRUE.
IF(PRESENT(PGPUV))   LLPGPUV = .TRUE.
IF(PRESENT(PGP3A))   LLPGP3A = .TRUE.
IF(PRESENT(PGP3B))   LLPGP3B = .TRUE.
IF(PRESENT(PGP2))    LLPGP2 = .TRUE.
IUVPAR=0
IUVLEV=0
IOFF1=0
IOFFNS=KF_SCALARS_G
IOFFEW=2*KF_SCALARS_G
LLUV(:) = .FALSE.
IUVPARS(:) = -99
IUVLEVS(:) = -99
IF (LLPGPUV) THEN
  IOFF=0
  IUVLEV=UBOUND(PGPUV,2)
  IF(LVORGP) THEN
    IUVPAR=IUVPAR+1
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR
      LLUV(IOFF+J)=.TRUE.
    ENDDO
    IOFF=IOFF+IUVLEV
  ENDIF
  IF(LDIVGP) THEN
    IUVPAR=IUVPAR+1
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR
      LLUV(IOFF+J)=.TRUE.
    ENDDO
    IOFF=IOFF+IUVLEV
  ENDIF
  DO J=1,IUVLEV
    IUVLEVS(IOFF+J)=J
    IUVPARS(IOFF+J)=IUVPAR+1
    IUVLEVS(IOFF+J+IUVLEV)=J
    IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
  ENDDO
  IUVPAR=IUVPAR+2
  LLUV(IOFF+1:IOFF+2*IUVLEV)=.TRUE.
  IOFF=IOFF+2*IUVLEV
  IOFF1=IOFF
  IOFFNS=IOFFNS+IOFF
  IOFFEW=IOFFEW+IOFF
  IOFF=IUVPAR*IUVLEV+KF_SCALARS_G
  IF(LUVDER) THEN
    IF(LSCDERS) IOFF=IOFF+KF_SCALARS_G
    DO J=1,IUVLEV
      IUVLEVS(IOFF+J)=J
      IUVPARS(IOFF+J)=IUVPAR+1
      LLUV(IOFF+J)=.TRUE.
      IUVLEVS(IOFF+J+IUVLEV)=J
      IUVPARS(IOFF+J+IUVLEV)=IUVPAR+2
      LLUV(IOFF+J+IUVLEV)=.TRUE.
    ENDDO
    IUVPAR=IUVPAR+2
    IOFF=IOFF+2*IUVLEV
    IOFFEW=IOFFEW+2*IUVLEV
  ENDIF
ENDIF
LLGP2(:)=.FALSE.
IF(LLPGP2) THEN
  IOFF=IOFF1
  IGP2PAR=UBOUND(PGP2,2)
  IF(LSCDERS) IGP2PAR=IGP2PAR/3
  DO J=1,IGP2PAR
    LLGP2(J+IOFF) = .TRUE.
    IGP2PARS(J+IOFF)=J
  ENDDO
  IOFF1=IOFF1+IGP2PAR
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J=1,IGP2PAR
      LLGP2(J+IOFF) = .TRUE.
      IGP2PARS(J+IOFF)=J+IGP2PAR
    ENDDO
    IOFFNS=IOFF+IGP2PAR
    IOFF=IOFFEW
    DO J=1,IGP2PAR
      LLGP2(J+IOFF) = .TRUE.
      IGP2PARS(J+IOFF)=J+2*IGP2PAR
    ENDDO
    IOFFEW=IOFF+IGP2PAR
  ENDIF
ENDIF
LLGP3A(:) = .FALSE.
IF(LLPGP3A) THEN
  IGP3ALEV=UBOUND(PGP3A,2)
  IGP3APAR=UBOUND(PGP3A,3)
  IF(LSCDERS) IGP3APAR=IGP3APAR/3
  IOFF=IOFF1
  DO J1=1,IGP3APAR
    DO J2=1,IGP3ALEV
      LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
      IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1
      IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
    ENDDO
  ENDDO
  IPAROFF=IGP3APAR
  IOFF1=IOFF1+IGP3APAR*IGP3ALEV
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J1=1,IGP3APAR
      DO J2=1,IGP3ALEV
        LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
        IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
        IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IPAROFF+IGP3APAR
    IOFFNS=IOFFNS+IGP3APAR*IGP3ALEV
    IOFF=IOFFEW
    DO J1=1,IGP3APAR
      DO J2=1,IGP3ALEV
        LLGP3A(J2+(J1-1)*IGP3ALEV+IOFF) = .TRUE.
        IGP3APARS(J2+(J1-1)*IGP3ALEV+IOFF)=J1+IPAROFF
        IGP3ALEVS(J2+(J1-1)*IGP3ALEV+IOFF)=J2
      ENDDO
    ENDDO
    IOFFEW=IOFFEW+IGP3APAR*IGP3ALEV
  ENDIF
ENDIF
LLGP3B(:) = .FALSE.
IF(LLPGP3B) THEN
  IGP3BLEV=UBOUND(PGP3B,2)
  IGP3BPAR=UBOUND(PGP3B,3)
  IF(LSCDERS) IGP3BPAR=IGP3BPAR/3
  IOFF=IOFF1
  DO J1=1,IGP3BPAR
    DO J2=1,IGP3BLEV
      LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
      IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1
      IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
    ENDDO
  ENDDO
  IPAROFF=IGP3BPAR
  IOFF1=IOFF1+IGP3BPAR*IGP3BLEV
  IF(LSCDERS) THEN
    IOFF=IOFFNS
    DO J1=1,IGP3BPAR
      DO J2=1,IGP3BLEV
        LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
        IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
        IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
      ENDDO
    ENDDO
    IPAROFF=IPAROFF+IGP3BPAR
    IOFFNS=IOFFNS+IGP3BPAR*IGP3BLEV
    IOFF=IOFFEW
    DO J1=1,IGP3BPAR
      DO J2=1,IGP3BLEV
        LLGP3B(J2+(J1-1)*IGP3BLEV+IOFF) = .TRUE.
        IGP3BPARS(J2+(J1-1)*IGP3BLEV+IOFF)=J1+IPAROFF
        IGP3BLEVS(J2+(J1-1)*IGP3BLEV+IOFF)=J2
      ENDDO
    ENDDO
    IOFFEW=IOFFEW+IGP3BPAR*IGP3BLEV
  ENDIF
ENDIF


CALL INIGPTR(IGPTRSEND,IGPTRRECV)
LLDONE = .FALSE.

ITAG = MTAGGL

INDOFFX  = 0
IBUFLENS = 0
IBUFLENR = 0
INRECV   = 0
INSEND   = 0

DO JROC=1,NPROC

  CALL PE2SET(JROC,ISETA,ISETB,ISETW,ISETV)
  IRECVSET = ISETA
  ISEND = JROC
  ISENDSET = ISETV
  ISENT(JROC) = 0
  IRCVD(JROC) = 0

!             count up expected number of fields
  IPOS = 0
  DO JFLD=1,KF_GP
    IF(KVSET(JFLD) == ISENDSET .OR. KVSET(JFLD) == -1) IPOS = IPOS+1
  ENDDO
  ISEND_FLD_TOTAL(JROC) = IPOS
  ISENDTOT(JROC) = IGPTRRECV(ISETW)*IPOS

  IF( JROC /= MYPROC) THEN
    IBUFLENS = MAX(IBUFLENS,ISENDTOT(JROC))
    IF(ISENDTOT(JROC) > 0) THEN
      INSEND = INSEND+1
      JSEND(INSEND)=JROC
    ENDIF
  ENDIF

  IFIRSTLAT = MAX(D%NPTRLS(MYSETW),D%NFRSTLAT(IRECVSET))
  ILASTLAT  = MIN(D%NPTRLS(MYSETW)+D%NULTPP(MYSETW)-1,D%NLSTLAT(IRECVSET))

  IPOS = 0
  DO JGL=IFIRSTLAT,ILASTLAT
    IGL  = D%NPTRFRSTLAT(IRECVSET)+JGL-D%NFRSTLAT(IRECVSET)
    IPOS = IPOS+D%NONL(IGL,ISETB)
  ENDDO

  IRECVTOT(JROC) = IPOS*KF_FS

  IF(IRECVTOT(JROC) > 0 .AND. MYPROC /= JROC) THEN
    INRECV = INRECV + 1
    JRECV(INRECV)=JROC
  ENDIF

  IF( JROC /= MYPROC) IBUFLENR = MAX(IBUFLENR,IRECVTOT(JROC))

  IF(IPOS > 0) THEN
    INDOFF(JROC) = INDOFFX
    INDOFFX = INDOFFX+IPOS
    IPOS = 0
    DO JGL=IFIRSTLAT,ILASTLAT
      IGL  = D%NPTRFRSTLAT(IRECVSET)+JGL-D%NFRSTLAT(IRECVSET)
      IGLL = JGL-D%NPTRLS(MYSETW)+1
      DO JL=D%NSTA(IGL,ISETB)+D%NSTAGTF(IGLL),&
       &D%NSTA(IGL,ISETB)+D%NSTAGTF(IGLL)+D%NONL(IGL,ISETB)-1
        IPOS = IPOS+1
        INDEX(IPOS+INDOFF(JROC)) = JL
      ENDDO
    ENDDO
  ENDIF

ENDDO

IPROCS_COMM_MAX = (NPRTRNS/NPRGPNS+1)*NPRTRV

ICOMBFLEN = NCOMBFLEN/IPROCS_COMM_MAX

IF( .NOT.LIMP.AND..NOT.LIMP_NOOLAP) THEN
  INSEND = 1
  INRECV = 1
  INS = 1
  INR = 1
ENDIF

IF(LIMP_NOOLAP)THEN
  ISENDCOUNT=0
  IRECVCOUNT=0
  DO J=1,NPROC
    ISENDCOUNT=MAX(ISENDCOUNT,ISENDTOT(J))
    IRECVCOUNT=MAX(IRECVCOUNT,IRECVTOT(J))
  ENDDO
  IF (IBUFLENS > 0) ALLOCATE(ZCOMBUFS(-1:ISENDCOUNT,INSEND))
  IF (IBUFLENR > 0) ALLOCATE(ZCOMBUFR(-1:IRECVCOUNT,INRECV))
ELSE
  IF (IBUFLENS > 0) ALLOCATE(ZCOMBUFS(-1:ICOMBFLEN,INSEND))
  IF (IBUFLENR > 0) ALLOCATE(ZCOMBUFR(-1:ICOMBFLEN,INRECV))
ENDIF

! Send loop.............................................................

IF(LIMP_NOOLAP)THEN

! Copy local contribution

  IF(ISENDTOT(MYPROC) > 0 )THEN
    IFLDS = 0
    DO JFLD=1,KF_GP
      IF(KVSET(JFLD) == MYSETV .OR. KVSET(JFLD) == -1) THEN
        IFLDS = IFLDS+1
        IF(LLINDER) THEN
          IFLDOFF(IFLDS) = KPTRGP(JFLD)
        ELSE
          IFLDOFF(IFLDS) = JFLD
        ENDIF
      ENDIF
    ENDDO

    IPOS=0
    DO JBLK=1,NGPBLKS
      IGPTROFF(JBLK)=IPOS
      IFIRST = IGPTRSEND(1,JBLK,MYSETW)
      IF(IFIRST > 0) THEN
        ILAST = IGPTRSEND(2,JBLK,MYSETW)
        IPOS=IPOS+ILAST-IFIRST+1
      ENDIF
    ENDDO
    IF (.NOT.LSYNCSTATS) CALL GSTATS(1609,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JFLD,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
    DO JBLK=1,NGPBLKS
      IFIRST = IGPTRSEND(1,JBLK,MYSETW)
      IF(IFIRST > 0) THEN
        ILAST = IGPTRSEND(2,JBLK,MYSETW)
        
        IF(LLPGPONLY) THEN
          DO JFLD=1,IFLDS
            IFLD = IFLDOFF(JFLD)
            DO JK=IFIRST,ILAST
              IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
              PGLAT(JFLD,INDEX(IPOS)) = PGP(JK,IFLD,JBLK)
            ENDDO
          ENDDO
        ELSE
          DO JFLD=1,IFLDS
            IFLD = IFLDOFF(JFLD)
            IF(LLUV(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                PGLAT(JFLD,INDEX(IPOS)) = PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK) 
              ENDDO
            ELSEIF(LLGP2(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                PGLAT(JFLD,INDEX(IPOS)) = PGP2(JK,IGP2PARS(IFLD),JBLK)
              ENDDO
            ELSEIF(LLGP3A(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                PGLAT(JFLD,INDEX(IPOS)) = PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)
              ENDDO
            ELSEIF(LLGP3B(IFLD)) THEN
              DO JK=IFIRST,ILAST
                IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                PGLAT(JFLD,INDEX(IPOS)) = PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)
              ENDDO
            ELSE
              WRITE(NOUT,*)'TRLTOG_MOD: ERROR',JFLD,IFLD
              CALL ABORT_TRANS('TRLTOG_MOD: ERROR')
            ENDIF
          ENDDO
        ENDIF
      ENDIF
    ENDDO
!$OMP END PARALLEL DO
    IF (.NOT.LSYNCSTATS) CALL GSTATS(1609,1)

  ENDIF

!....Pack loop.........................................................
    ISEND_FLD_START=1
  DO INS=1,INSEND
    ISEND=JSEND(INS)
    CALL PE2SET(ISEND,ISETA,ISETB,ISETW,ISETV)
    ISENDSET = ISETV
    ISEND_FLD_END = ISEND_FLD_TOTAL(ISEND)
    IFLD = 0
    IPOS = 0
    CALL GTOL_PACK
    ZCOMBUFS(-1,INS) = 1
    ZCOMBUFS(0,INS) = IFLD
  ENDDO

!....Send loop.........................................................

  IF (.NOT.LSYNCSTATS) CALL GSTATS(802,0)
  DO INS=1,INSEND
    ISEND=JSEND(INS)
    CALL MPL_SEND(ZCOMBUFS(-1:ISENDTOT(ISEND),INS),KDEST=NPRCIDS(ISEND), &
     & KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=ISENDREQ(INS), &
     & KTAG=ITAG,CDSTRING='TRGTOL:' )
  ENDDO
  IF (.NOT.LSYNCSTATS) CALL GSTATS(802,1)

!  Receive loop.........................................................

  IF (.NOT.LSYNCSTATS) CALL GSTATS(803,0)
  DO INR=INRECV,1,-1
    IRECV=JRECV(INR)
    CALL MPL_RECV(ZCOMBUFR(-1:IRECVTOT(IRECV),INR), &
      & KSOURCE=NPRCIDS(IRECV), &
      & KMP_TYPE=JP_BLOCKING_STANDARD,KOUNT=ILRECV(IRECV), &
      & KTAG=ITAG,CDSTRING='TRLTOG:' )
  ENDDO

  IF(INSEND > 0) THEN
    CALL MPL_WAIT(ZCOMBUFS(-1:ISENDCOUNT,1),KREQUEST=ISENDREQ(1:INSEND), &
      & CDSTRING='TRGTOL: WAIT FOR SENDS')
  ENDIF
  IF (.NOT.LSYNCSTATS) CALL GSTATS(803,1)

!  Unpack loop.........................................................
  DO INR=1,INRECV
    IRECV=JRECV(INR)
    ILEN = IRECVTOT(IRECV)/KF_FS
    IRECV_FLD_START = ZCOMBUFR(-1,INR)
    IRECV_FLD_END   = ZCOMBUFR(0,INR)
    CALL GTOL_UNPACK
    IPOS = ILEN*(IRECV_FLD_END-IRECV_FLD_START+1)
    IF (ILRECV(IRECV) /= IPOS+2) THEN
      WRITE(NOUT,*) MYPROC,MYSETV, &
        & ' receiving ',ILRECV(IRECV),'expected',IPOS
      CALL ABORT_TRANS('TRGTOL: RECEIVED MESSAGE OF INCORRECT LENGTH')
    ENDIF
  ENDDO

ELSE

  ISEND_FLD_START(:) = 1
  
  DO WHILE( .NOT.LLDONE )
  
    LLDONE = .TRUE.
!  For immediate send/recv, post receives first
  
    IF(LIMP) THEN
      INR = 0
      DO JLOOP=1,NPROC-1
        IPROC = MYRECVSET(NPROC,MYPROC,JLOOP)
  
! Check if there is data to receive
  
        IF( IRECVTOT(IPROC)-IRCVD(IPROC) > 0 )THEN
          IRECV = IPROC
          INR = INR+1
  
!*   receive message
  
          CALL MPL_RECV(ZCOMBUFR(-1:ICOMBFLEN,INR),KSOURCE=NPRCIDS(IRECV), &
           & KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=IRECVREQ(INR), &
           & KTAG=ITAG,CDSTRING='TRLTOG:' )
        ENDIF
      ENDDO
    ENDIF
!
! loop over the number of processors we need to communicate with
!
    IF(LIMP) INS = 0
  
    DO JLOOP=1,NPROC-1
      IPROC = MYSENDSET(NPROC,MYPROC,JLOOP)
  
! Check if we have data to send
  
      IF(ISENDTOT(IPROC)-ISENT(IPROC) > 0 )THEN
        LLDONE = .FALSE.
  
        CALL PE2SET(IPROC,ISETA,ISETB,ISETW,ISETV)
        ISEND = IPROC
        ISENDSET = ISETV
        IF(LIMP) INS = INS+1
  
        INUMFLDS = ICOMBFLEN/IGPTRRECV(ISETW)
  
        ISEND_FLD_END = MIN(ISEND_FLD_TOTAL(IPROC),&
         &ISEND_FLD_START(IPROC)+INUMFLDS-1)
  
        ZCOMBUFS(-1,INS) = ISEND_FLD_START(IPROC)
        IFLD = 0
        IPOS = 0
        CALL GTOL_PACK
        ZCOMBUFS(0,INS) = ISEND_FLD_END
  
        ISEND_FLD_START(IPROC) = ISEND_FLD_END+1
  
        IF(LIMP) THEN
          CALL MPL_SEND(ZCOMBUFS(-1:IPOS,INS),KDEST=NPRCIDS(ISEND), &
           & KMP_TYPE=JP_NON_BLOCKING_STANDARD,KREQUEST=ISENDREQ(INS), &
           & KTAG=ITAG,CDSTRING='TRGTOL:' )
        ELSE
          CALL MPL_SEND(ZCOMBUFS(-1:IPOS,INS),KDEST=NPRCIDS(ISEND), &
           &    KTAG=ITAG,CDSTRING='TRGTOL:' )
        ENDIF
        ISENT(IPROC) = ISENT(IPROC)+IPOS
  
      ENDIF
    ENDDO
  
! Copy local data
  
    IF(ISENDTOT(MYPROC)-ISENT(MYPROC) > 0 )THEN
  
      IFLDS = 0
      DO JFLD=1,KF_GP
        IF(KVSET(JFLD) == MYSETV .OR. KVSET(JFLD) == -1) THEN
          IFLDS = IFLDS+1
          IF(LLINDER) THEN
            IFLDOFF(IFLDS) = KPTRGP(JFLD)
          ELSE
            IFLDOFF(IFLDS) = JFLD
          ENDIF
        ENDIF
      ENDDO
  
      IPOS=0
      DO JBLK=1,NGPBLKS
        IGPTROFF(JBLK)=IPOS
        IFIRST = IGPTRSEND(1,JBLK,MYSETW)
        IF(IFIRST > 0) THEN
          ILAST = IGPTRSEND(2,JBLK,MYSETW)
          IPOS=IPOS+ILAST-IFIRST+1
        ENDIF
      ENDDO
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JFLD,JBLK,JK,IFLD,IPOS,IFIRST,ILAST)
      DO JBLK=1,NGPBLKS
        IFIRST = IGPTRSEND(1,JBLK,MYSETW)
        IF(IFIRST > 0) THEN
          ILAST = IGPTRSEND(2,JBLK,MYSETW)
  
          IF(LLPGPONLY) THEN
            DO JFLD=1,IFLDS
              IFLD = IFLDOFF(JFLD)
              DO JK=IFIRST,ILAST
                IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                PGLAT(JFLD,INDEX(IPOS)) = PGP(JK,IFLD,JBLK)
              ENDDO
            ENDDO
          ELSE
            DO JFLD=1,IFLDS
              IFLD = IFLDOFF(JFLD)
              IF(LLUV(IFLD)) THEN
                DO JK=IFIRST,ILAST
                  IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                  PGLAT(JFLD,INDEX(IPOS)) = PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK)
                ENDDO
              ELSEIF(LLGP2(IFLD)) THEN
                DO JK=IFIRST,ILAST
                  IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                  PGLAT(JFLD,INDEX(IPOS)) = PGP2(JK,IGP2PARS(IFLD),JBLK)
                ENDDO
              ELSEIF(LLGP3A(IFLD)) THEN
                DO JK=IFIRST,ILAST
                  IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                  PGLAT(JFLD,INDEX(IPOS)) = PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)
                ENDDO
              ELSEIF(LLGP3B(IFLD)) THEN
                DO JK=IFIRST,ILAST
                  IPOS = INDOFF(MYPROC)+IGPTROFF(JBLK)+JK-IFIRST+1
                  PGLAT(JFLD,INDEX(IPOS)) = PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)
                ENDDO
              ELSE
                WRITE(NOUT,*)'TRLTOG_MOD: ERROR',JFLD,IFLD
                CALL ABORT_TRANS('TRLTOG_MOD: ERROR')
              ENDIF
            ENDDO
          ENDIF
        ENDIF
      ENDDO
!$OMP END PARALLEL DO
  
      ISENT(MYPROC) = ISENDTOT(MYPROC)
  
    ENDIF
  
!  Receive loop.........................................................
  
    IF (LIMP) INR = 0
  
    DO JLOOP=1,NPROC-1
      IPROC = MYRECVSET(NPROC,MYPROC,JLOOP)
      IF( IRECVTOT(IPROC)-IRCVD(IPROC) > 0 )THEN
        LLDONE = .FALSE.
        CALL PE2SET(IPROC,ISETA,ISETB,ISETW,ISETV)
        IRECVSET = ISETA
        IRECV = IPROC
!*   receive message
  
  
        IF (LIMP) THEN
          INR = INR+1
          CALL MPL_WAIT(ZCOMBUFR(-1:ICOMBFLEN,INR),KREQUEST=IRECVREQ(INR), &
           & KCOUNT=ILRECV(JLOOP),CDSTRING='TRGTOL: LIMP WAITS ' )
        ELSE
          CALL MPL_RECV(ZCOMBUFR(-1:ICOMBFLEN,INR),KSOURCE=NPRCIDS(IRECV), &
           & KTAG=ITAG,KOUNT=ILRECV(JLOOP),CDSTRING='TRGTOL:' )
        ENDIF
        IRECV_FLD_START = ZCOMBUFR(-1,INR)
        IRECV_FLD_END   = ZCOMBUFR(0,INR)
  
  
!*   store data in Fourier array
  
        ILEN = IRECVTOT(IRECV)/KF_FS
        CALL GTOL_UNPACK
        IPOS = ILEN*(IRECV_FLD_END-IRECV_FLD_START+1)
        IF (ILRECV(JLOOP) /= IPOS+2) THEN
          WRITE(NOUT,*) MYPROC,MYSETV,' receiving ',ILRECV(JLOOP),'expected',IPOS
          CALL ABORT_TRANS('TRGTOL: RECEIVED MESSAGE OF INCORRECT LENGTH')
        ENDIF
        IRCVD(IPROC) = IRCVD(IPROC)+IPOS
  
      ENDIF
    ENDDO
  
    IF(LIMP)THEN
      IF(INS > 0) THEN
        CALL MPL_WAIT(ZCOMBUFS(-1:ICOMBFLEN,1),KREQUEST=ISENDREQ(1:INS), &
        & CDSTRING='TRGTOL: ERROR IN MPL_WAIT FOR SENDS')
      ENDIF
    ENDIF
  
  ENDDO

! Perform barrier synchronisation to guarantee all processors have
! completed communication

  IF( NPROC > 1)THEN
    CALL MPL_BARRIER(CDSTRING='TRGTOL:')
  ENDIF
ENDIF

IF (IBUFLENS > 0) DEALLOCATE(ZCOMBUFS)
IF (IBUFLENR > 0) DEALLOCATE(ZCOMBUFR)


!     ------------------------------------------------------------------
RETURN
CONTAINS

SUBROUTINE GTOL_PACK

IFLD=0
IPOS=0
DO JFLD=1,KF_GP
  IF(KVSET(JFLD) == ISENDSET .OR. KVSET(JFLD) == -1 ) THEN
    IFLD = IFLD+1
    IFLDA(IFLD)=JFLD
  ENDIF
ENDDO

DO JBLK=1,NGPBLKS
  IFIRST = IGPTRSEND(1,JBLK,ISETW)
  IF(IFIRST > 0) THEN
    ILAST = IGPTRSEND(2,JBLK,ISETW)
    IJPOS(JBLK)=IPOS
    IPOS=IPOS+(ILAST-IFIRST+1)
  ENDIF
ENDDO

    
IF (.NOT.LSYNCSTATS) CALL GSTATS(1602,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(IFLD,JBLK,IFIRST,ILAST,JK,JJ,JI)
DO JJ=ISEND_FLD_START(ISEND),ISEND_FLD_END
  IFLD=IFLDA(JJ)
  DO JBLK=1,NGPBLKS
    IFIRST = IGPTRSEND(1,JBLK,ISETW)
    IF(IFIRST > 0) THEN
      ILAST = IGPTRSEND(2,JBLK,ISETW)
      IF(LLINDER) THEN
        DO JK=IFIRST,ILAST
          JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
          ZCOMBUFS(JI,INS) = PGP(JK,KPTRGP(IFLD),JBLK)
        ENDDO
      ELSE
        IF(LLPGPONLY) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
            ZCOMBUFS(JI,INS) = PGP(JK,IFLD,JBLK)
          ENDDO
        ELSEIF(LLUV(IFLD)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
            ZCOMBUFS(JI,INS) = PGPUV(JK,IUVLEVS(IFLD),IUVPARS(IFLD),JBLK)
          ENDDO
        ELSEIF(LLGP2(IFLD)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
            ZCOMBUFS(JI,INS) = PGP2(JK,IGP2PARS(IFLD),JBLK)
          ENDDO
        ELSEIF(LLGP3A(IFLD)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
            ZCOMBUFS(JI,INS) = PGP3A(JK,IGP3ALEVS(IFLD),IGP3APARS(IFLD),JBLK)
          ENDDO
        ELSEIF(LLGP3B(IFLD)) THEN
          DO JK=IFIRST,ILAST
            JI=(JJ-ISEND_FLD_START(ISEND))*IPOS+IJPOS(JBLK)+JK-IFIRST+1
            ZCOMBUFS(JI,INS) = PGP3B(JK,IGP3BLEVS(IFLD),IGP3BPARS(IFLD),JBLK)
          ENDDO
        ENDIF
      ENDIF
    ENDIF
  ENDDO
ENDDO
!$OMP END PARALLEL DO
IF (.NOT.LSYNCSTATS) CALL GSTATS(1602,1)

IPOS=(ISEND_FLD_END-ISEND_FLD_START(ISEND)+1)*IPOS

END SUBROUTINE GTOL_PACK

SUBROUTINE GTOL_UNPACK

IF (.NOT.LSYNCSTATS) CALL GSTATS(1603,0)
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(JL,II,JFLD)
DO JL=1,ILEN
  II = INDEX(INDOFF(IRECV)+JL)
  DO JFLD=IRECV_FLD_START,IRECV_FLD_END
    PGLAT(JFLD,II) = ZCOMBUFR(JL+(JFLD-IRECV_FLD_START)*ILEN,INR)
  ENDDO
ENDDO
!$OMP END PARALLEL DO
IF (.NOT.LSYNCSTATS) CALL GSTATS(1603,1)
END SUBROUTINE GTOL_UNPACK

END SUBROUTINE TRGTOL
END MODULE TRGTOL_MOD





