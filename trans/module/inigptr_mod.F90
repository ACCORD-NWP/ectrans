MODULE INIGPTR_MOD
CONTAINS
SUBROUTINE INIGPTR(KGPTRSEND,KGPTRRECV)

!     Compute tables to assist GP to/from Fourier space transpositions

#include "tsmbkind.h"

USE TPM_GEN
USE TPM_DISTR
USE TPM_TRANS
USE ABORT_TRANS_MOD

IMPLICIT NONE

INTEGER_M,INTENT(OUT) :: KGPTRSEND(2,NGPBLKS,NPRTRNS)
INTEGER_M,INTENT(OUT) :: KGPTRRECV(NPRTRNS)

INTEGER_M :: IBLOCK,IROF,IBFIRST,IPROCLAST,IPROC,IFIRST,ILAST,IBLAST
INTEGER_M :: JGL,JBL,JPRTRNS,JBLKS
!     Compute tables to assist GP to/from Fourier space transpositions


KGPTRSEND(:,:,:)=0
IBLOCK=1
IROF=1
IBFIRST=1
IPROCLAST=D%NPROCL(D%NFRSTLOFF+1)
DO JGL=1,D%NDGL_GP
  !   Find processor which deals with this latitude in Fourier distribution
  IPROC=D%NPROCL(D%NFRSTLOFF+JGL)
  IF(IPROC > NPRTRNS) THEN
    WRITE(NOUT,'(A,I8)')&
     &' INIGPTR ERROR : exceeding processor limit ',NPRTRNS
    CALL ABORT_TRANS(' INIGPTR ERROR : exceeding processor limit ')
  ENDIF

  !           for each latitude on this processor, find first and last points
  !           for each NPROMA chunk, for each destination processor
  IF(IPROC /= IPROCLAST) THEN
    IF(IROF > 1) THEN
      KGPTRSEND(1,IBLOCK,IPROCLAST)=IBFIRST
      KGPTRSEND(2,IBLOCK,IPROCLAST)=IROF-1
    ENDIF
    IF(IROF <= NPROMA) IBFIRST=IROF
    IPROCLAST=IPROC
  ENDIF
  IFIRST=D%NSTA(D%NPTRFLOFF+JGL,MYSETEW)
  ILAST =IFIRST + D%NONL(D%NPTRFLOFF+JGL,MYSETEW) -1
  DO JBL=IFIRST,ILAST
    IF(IROF == NPROMA) THEN
      IBLAST=IROF
      KGPTRSEND(1,IBLOCK,IPROC)=IBFIRST
      KGPTRSEND(2,IBLOCK,IPROC)=IBLAST
      IF(IBLOCK < NGPBLKS) IBLOCK=IBLOCK+1
      IROF=0
      IBFIRST=1
    ENDIF
    IROF=IROF+1
  ENDDO
ENDDO
IF(IROF /= 1.AND.IROF /= IBFIRST) THEN
!           non-empty residual block after last latitude line
  IBLAST=IROF-1
  KGPTRSEND(1,IBLOCK,IPROC)=IBFIRST
  KGPTRSEND(2,IBLOCK,IPROC)=IBLAST
ENDIF
!         sum up over blocks
KGPTRRECV(:)=0
DO JPRTRNS=1,NPRTRNS
  DO JBLKS=1,NGPBLKS
    IF(KGPTRSEND(1,JBLKS,JPRTRNS) > 0) THEN
      KGPTRRECV(JPRTRNS)=KGPTRRECV(JPRTRNS)+&
       &KGPTRSEND(2,JBLKS,JPRTRNS)-KGPTRSEND(1,JBLKS,JPRTRNS)+1
    ENDIF
  ENDDO
ENDDO

END SUBROUTINE INIGPTR
END MODULE INIGPTR_MOD
