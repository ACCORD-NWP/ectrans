MODULE LTINV_CTL_MOD
CONTAINS
SUBROUTINE LTINV_CTL(PSPVOR,PSPDIV,PSPSCALAR,FSPGL_PROC)

#include "tsmbkind.h"

USE TPM_GEN
USE TPM_DIM
USE TPM_TRANS
USE TPM_DISTR

USE LTINV_MOD
USE TRMTOL_MOD

IMPLICIT NONE

REAL_B ,OPTIONAL, INTENT(IN) :: PSPVOR(:,:)
REAL_B ,OPTIONAL, INTENT(IN) :: PSPDIV(:,:)
REAL_B ,OPTIONAL, INTENT(IN) :: PSPSCALAR(:,:)
EXTERNAL  FSPGL_PROC
OPTIONAL  FSPGL_PROC

INTEGER_M :: JM,IM,IBLEN

!     ------------------------------------------------------------------

NLEI2 = 8*NF_UV + 2*NF_SCALARS + 2*NF_SCDERS
IBLEN = D%NLENGT0B*2*NF_OUT_LT
IF(LALLOPERM) THEN
  IF(ALLOCATED(FOUBUF_IN)) THEN
    IF(SIZE(FOUBUF_IN) < IBLEN) THEN
      DEALLOCATE(FOUBUF_IN)
    ENDIF
  ENDIF
ENDIF
IF(.NOT. ALLOCATED(FOUBUF_IN)) ALLOCATE(FOUBUF_IN(IBLEN))

CALL GSTATS(102,0)
!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JM,IM)
DO JM=1,D%NUMP
  IM = D%MYMS(JM)
  CALL LTINV(IM,JM,PSPVOR,PSPDIV,PSPSCALAR,FSPGL_PROC)
ENDDO
!$OMP END PARALLEL DO
CALL GSTATS(102,1)

IBLEN = D%NLENGT0B*2*NF_OUT_LT
IF(LALLOPERM) THEN
  IF(ALLOCATED(FOUBUF)) THEN
    IF(SIZE(FOUBUF) < IBLEN) THEN
      DEALLOCATE(FOUBUF)
    ENDIF
  ENDIF
ENDIF
IF(.NOT. ALLOCATED(FOUBUF))  ALLOCATE(FOUBUF(IBLEN))

CALL GSTATS(152,0)
CALL TRMTOL(FOUBUF_IN,FOUBUF,2*NF_OUT_LT)
CALL GSTATS(152,1)
IF(.NOT. LALLOPERM) THEN
  DEALLOCATE(FOUBUF_IN)
ENDIF

!     ------------------------------------------------------------------

END SUBROUTINE LTINV_CTL
END MODULE LTINV_CTL_MOD
