MODULE DIST_SPEC_CONTROL_MOD
CONTAINS
SUBROUTINE DIST_SPEC_CONTROL(PSPECG,KFDISTG,KFROM,KVSET,PSPEC)

#include "tsmbkind.h"

USE TPM_GEN
USE TPM_DIM
USE TPM_DISTR

USE SET2PE_MOD

IMPLICIT NONE

REAL_B    ,OPTIONAL, INTENT(IN) :: pspecg(:,:)
INTEGER_M          , INTENT(IN) :: kfdistg
INTEGER_M          , INTENT(IN) :: kfrom(:)
INTEGER_M          , INTENT(IN) :: KVSET(:)
REAL_B    ,OPTIONAL, INTENT(OUT) :: pspec(:,:)

INTEGER_M :: IDIST(R%NSPEC2_G)
REAL_B :: ZFLD(R%NSPEC2_G)
INTEGER_M :: JM,JN,II,IFLDR,IFLDS,JFLD,ITAG,JNM,IBSET,ILEN,JA,IERR,ISND
INTEGER_M :: ISENDER,ITAGR,IRCV


II = 0
DO JM=0,R%NSMAX
  DO JN=JM,R%NSMAX
    IDIST(II+1) = D%NDIM0G(JM)+(JN-JM)*2
    IDIST(II+2) = D%NDIM0G(JM)+(JN-JM)*2+1
    II = II+2
  ENDDO
ENDDO

IFLDR = 0
IFLDS = 0

DO JFLD=1,KFDISTG

  IBSET = KVSET(JFLD)

  IF(KFROM(JFLD) == MYPROC) THEN
    IFLDS = IFLDS+1
    DO JNM=1,R%NSPEC2_G
      ZFLD(IDIST(JNM)) = PSPECG(IFLDS,JNM) 
    ENDDO
    DO JA=1,NPRTRW
      ILEN = D%NPOSSP(JA+1)-D%NPOSSP(JA)
      IF( ILEN > 0 )THEN
        CALL SET2PE(ISND,0,0,JA,IBSET)
        ITAG = MTAGDISTSP+JFLD
        IF( NPROC > 1 )THEN
          CALL MPE_SEND(ZFLD(D%NPOSSP(JA)),ILEN,MREALT,&
           &NPRCIDS(ISND),ITAG,0,0,0,IERR)
          IF( IERR < 0 )THEN
            CALL ABOR1(' DIST_SPEC_CONTROL : ERROR IN MPE_SEND (ZFLD)')
          ENDIF
        ENDIF
      ENDIF
    ENDDO
  ENDIF

  IF( IBSET == MYSETV )THEN

    IF( D%NSPEC2 > 0 )THEN
      IRCV = KFROM(JFLD)
      ITAG = MTAGDISTSP+JFLD
      IF( NPROC > 1 )THEN
        CALL MPE_RECV(ZFLD,D%NSPEC2,MREALT,NPRCIDS(IRCV),ITAG,0,&
         &0,0,ILEN,ISENDER,ITAGR,IERR)
        IF( IERR < 0 )THEN
          CALL ABOR1(' DIST_SPEC_CONTROL : ERROR IN MPE_RECV (ZFLD)')
        ENDIF
        IF( ILEN /= D%NSPEC2 )THEN
          CALL ABOR1(' DIST_SPEC_CONTROL: INVALID RECEIVE MESSAGE LENGTH')
        ENDIF
      ENDIF
    ENDIF
    IFLDR = IFLDR+1
    PSPEC(IFLDR,:) = ZFLD(1:D%NSPEC2)
  ENDIF

  IF( NPROC > 1 )THEN
    CALL MPE_BARRIER(IERR)
    IF( IERR /= 0 )THEN
      CALL ABOR1(' DIST_SPEC_CONTROL: ERROR IN MPE_BARRIER')
    ENDIF
  ENDIF

ENDDO

END SUBROUTINE DIST_SPEC_CONTROL
END MODULE DIST_SPEC_CONTROL_MOD


