MODULE FSC_MOD
CONTAINS
SUBROUTINE FSC(PUV,PSCALAR,PNSDERS,PEWDERS,PUVDERS)

#include "tsmbkind.h"

USE TPM_DISTR
USE TPM_FIELDS
USE TPM_TRANS
USE TPM_GEOMETRY

IMPLICIT NONE
 
REAL_B , INTENT(INOUT) :: PUV(:,:)
REAL_B , INTENT(IN   ) :: PSCALAR(:,:)
REAL_B , INTENT(INOUT) :: PNSDERS(:,:)
REAL_B , INTENT(  OUT) :: PEWDERS(:,:)
REAL_B , INTENT(  OUT) :: PUVDERS(:,:)

INTEGER_M :: JGL,JLON,JF,IGLG,II,IR,JM


!*       1.    DIVIDE U V AND N-S DERIVATIVES BY A*COS(THETA)
!              ----------------------------------------------

!$OMP PARALLEL DO SCHEDULE(STATIC,1) PRIVATE(JGL,JLON,JF,IGLG,IJ,II,IR,JM)
DO JGL=1,D%NDGL_FS
  IGLG = D%NPTRLS(MYSETW)+JGL-1
  
!*       1.1      U AND V.

  IF(NF_UV > 0) THEN
    DO JLON=D%NSTAGTF(JGL)+1,D%NSTAGTF(JGL)+2*(G%NMEN(IGLG)+1)
      DO JF=1,2*NF_UV
        PUV(JF,JLON) = PUV(JF,JLON)*F%RACTHE(IGLG)
      ENDDO
    ENDDO
  ENDIF

  IF(NF_SCDERS > 0)THEN
    DO JLON=D%NSTAGTF(JGL)+1,D%NSTAGTF(JGL)+2*(G%NMEN(IGLG)+1)
      DO JF=1,NF_SCALARS
        PNSDERS(JF,JLON) = PNSDERS(JF,JLON)*F%RACTHE(IGLG)
      ENDDO
    ENDDO
  ENDIF

!     ------------------------------------------------------------------

!*       2.    EAST-WEST DERIVATIVES
!              ---------------------

  IF(LUVDER)THEN
    DO JM=0,G%NMEN(IGLG)
      IR = D%NSTAGTF(JGL)+2*JM+1
      II = IR+1
      DO JF=1,2*NF_UV
          PUVDERS(JF,IR) = -JM*PUV(JF,II)*F%RACTHE(IGLG)
          PUVDERS(JF,II) =  JM*PUV(JF,IR)*F%RACTHE(IGLG)
      ENDDO
    ENDDO
  ENDIF

  IF(NF_SCDERS > 0)THEN
    DO JM=0,G%NMEN(IGLG)
      IR = D%NSTAGTF(JGL)+2*JM+1
      II = IR+1
      DO JF=1,NF_SCALARS
          PEWDERS(JF,IR) = -JM*PSCALAR(JF,II)*F%RACTHE(IGLG)
          PEWDERS(JF,II) =  JM*PSCALAR(JF,IR)*F%RACTHE(IGLG)
      ENDDO
    ENDDO
  ENDIF

ENDDO
!$OMP END PARALLEL DO

END SUBROUTINE FSC
END MODULE FSC_MOD
